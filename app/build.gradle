apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'org.jetbrains.dokka-android'

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.example.djung0.android_template"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            zipAlignEnabled true
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    ktlint
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // kotlin
    implementation deps.kotlin.stdlib.jre7

    // Timber
    implementation deps.timber

    // support library
    implementation deps.android.support.v4
    implementation deps.android.support.appcompat
    implementation deps.android.support.recyclerview
    implementation deps.android.support.cardview
    implementation deps.android.support.design
    implementation deps.android.support.emoji
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    // architecture components
    implementation deps.android.arch.runtime
    implementation deps.android.arch.extensions
    implementation deps.android.arch.reactivestreams
    kapt deps.android.arch.compiler

    // Glide
    implementation deps.glide.core
    implementation deps.glide.okhttp3
    kapt deps.glide.compiler

    // OkHttp3
    implementation deps.okhttp3.core
    implementation deps.okhttp3.loggingIntercepter

    // Retrofit
    implementation deps.retrofit.core
    implementation deps.retrofit.converterMoshi
    implementation deps.retrofit.adapterRxJava2

    // moshi
    implementation deps.moshi.core
    implementation deps.moshi.kotlin

    // Dagger2
    implementation deps.dagger.core
    implementation deps.dagger.android
    implementation deps.dagger.androidSupport
    kapt deps.dagger.compiler
    kapt deps.dagger.androidProcessor

    // Reactive X
    implementation deps.reactive.rxandroid
    implementation deps.reactive.rxjava2
    implementation deps.reactive.rxkotlin

    // ktlint
    ktlint deps.ktlint

    // ktx
    implementation deps.android.x.core

}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
